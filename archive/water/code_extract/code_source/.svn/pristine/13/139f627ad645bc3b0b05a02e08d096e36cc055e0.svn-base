// File Name: import_est_whosis.do

// File Purpose: Import coverage estimates from WHOSIS database
// Author: Leslie Mallinger
// Date: 4/10/10
// Edited on: 

// Additional Comments: 


clear all
macro drop _all
set mem 500m
set more off
capture log close


** create locals for relevant files and folders
local dat_folder_who "J:/Project/COMIND/Water and Sanitation/Data Audit/Data/WHO"
local codes_folder "J:/Usable/Common Indicators/Country Codes"


** open WHOSIS database, rename variables for clarity
insheet using "`dat_folder_who'/WHOSIS_03012010.csv", clear
rename location countryname
rename timeperiod startyear
rename v4 iwater_mean
rename v7 isanitation_mean


** keep only variables of interest, then generate values for other important variables
keep countryname startyear iwater_mean isanitation_mean
gen endyear = startyear
tostring endyear, replace
gen dattype = "REPORT"
gen svy = "WHOSIS"


** convert prevalence estimates to percentages
replace iwater_mean = iwater_mean/100
replace isanitation_mean = isanitation_mean/100
	
	
** match country names with iso codes, determine whether IHME country
merge m:1 countryname using "`codes_folder'/countrycodes_official.dta", keepusing(countryname countryname_ihme iso3 ihme_country)
drop if _merge == 2
drop if ihme_country == 0
drop countryname _merge
rename countryname_ihme countryname

	
** organize and save
order countryname iso3 ihme_country startyear endyear dattype svy, first
sort countryname startyear

cd "`dat_folder_who'"
save "prev_whosis", replace


capture log close