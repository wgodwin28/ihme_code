// File Name: extract_files_other.do

// File Purpose: Extract appropriate survey datasets from random ("other") surveys
// Author: Leslie Mallinger
// Date: 6/21/2010
// Edited on: 

// Additional Comments: 


clear all
macro drop _all
set mem 500m
set more off
capture log close


** create locals for relevant files and folders
local log_folder "J:/Project/COMIND/Water and Sanitation/Data Audit/Code"
local dat_folder_other "J:/Project/COMIND/Water and Sanitation/Data Audit/Data/Other/Merged Original Files"
local dat_folder_new_other "J:/Project/COMIND/Water and Sanitation/Data Audit/Data/Other"
local codes_folder "J:/Usable/Common Indicators/Country Codes"

capture mkdir "`dat_folder_new_other'"
capture mkdir "`dat_folder_other'"


** create local with household survey filenames in RHS data folder (must contain the letters "HH"), then make 
** one observation per filename
	// designate locals with filenames
	local filenames: dir "`dat_folder_other'" files "*", respectcase

	// designate locals with the number of files
	local numfiles: list sizeof filenames
	
	// put each filename into its own observation
	local numobs = `numfiles'
	set obs `numobs'
	local obsnum = 1
	gen filename = ""
	
	foreach file of local filenames {
		replace filename = "`file'" if _n == `obsnum'
		local obsnum = `obsnum' + 1
	}
	
	
** parse each part of the filename into informative variables
	local fileregex "^([a-zA-Z]+)[_]+([a-zA-Z]+[ ]*[a-zA-Z]*[ ]*[a-zA-Z]*)[_]+([0-9]+)"
	
	** [_]+([a-zA-Z]+)([0-9]+)[_]+([a-zA-Z]+)[-]*([a-zA-Z]*)[_]+([0-9]+)[-]*([0-9]*)[_]+([a-zA-Z]+)"

	// data type
	gen dattype = "CRUDE"
	
	// survey type
	gen svytype = "INT"
	
	// survey
	gen svy = regexs(1) if regexm(filename, "`fileregex'")
	replace svy = upper(svy)
	
	// countryname
	gen countryname = regexs(2) if regexm(filename, "`fileregex'")
	replace countryname = proper(countryname)
	
	// years
	gen startyear = regexs(3) if regexm(filename, "`fileregex'")
	gen endyear = startyear
	
	// filedir
	gen filedir = "`dat_folder_other'"
	
	// filepath_full
	gen filepath_full = filedir + "/" + filename
	
	
	
** match country names with iso3 codes, determine whether IHME country
preserve
use "`codes_folder'/countrycodes_official.dta", clear
keep countryname countryname_ihme iso3 ihme_country
drop if countryname == "Burma" & countryname_ihme == "Burma"
tempfile codes
save `codes', replace
restore

merge m:1 countryname using `codes'
drop if _merge == 2
drop _merge countryname
rename countryname_ihme countryname
	
	
** organize and save
order countryname iso3 ihme_country startyear endyear filepath_full filedir filename, first
sort countryname startyear

cd "`dat_folder_new_other'"
save "datfiles_other", replace


capture log close